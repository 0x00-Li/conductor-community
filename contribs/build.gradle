plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

configurations {
    shadow.extendsFrom implementation
}

dependencies {

    //the following modules are added to make contrib published jars backward compatible
    shadow project(':conductor-task')
    shadow project(':event-queue:conductor-nats')
    shadow project(':event-queue:conductor-amqp')
    shadow project(':lock:conductor-local-lock')
    shadow project(':conductor-metrics')

    implementation "com.netflix.conductor:conductor-common:${revConductor}"
    implementation "com.netflix.conductor:conductor-core:${revConductor}"

    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation project(':conductor-test-util').sourceSets.test.output
}

shadowJar {

    configurations = [project.configurations.shadow]
    classifier = null
    dependencies {
        include(dependency {
            it.moduleGroup == 'com.netflix.conductor' &&
                    (it.moduleName == 'conductor-task'
                            || it.moduleName == 'conductor-sqs'
                            || it.moduleName == 'conductor-nats'
                            || it.moduleName == 'conductor-amqp'
                            || it.moduleName == 'conductor-local-lock'
                            || it.moduleName == 'conductor-metrics'
                    )
        })
    }
    // Service files are not included by default.
    mergeServiceFiles {
        include 'META-INF/services/*'
        include 'META-INF/maven/*'
    }
}

jar.enabled = false
jar.dependsOn shadowJar