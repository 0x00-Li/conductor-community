plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

configurations {
    shadow.extendsFrom implementation
}

dependencies {

    //the following modules are added to make contrib published jars backward compatible
    shadow project(':conductor-task')
    shadow project(':event-queue:conductor-sqs')
    shadow project(':event-queue:conductor-nats')
    shadow project(':event-queue:conductor-amqp')
    shadow project(':lock:conductor-local-lock')
    shadow project(':conductor-metrics')
    shadow project(':index:conductor-noop')
    shadow project(':external-payload-storage:conductor-s3-storage')

    implementation "com.netflix.conductor:conductor-common:${revConductor}"
    implementation "com.netflix.conductor:conductor-core:${revConductor}"

    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework.boot:spring-boot-starter-web'

    implementation "com.amazonaws:aws-java-sdk-s3:${revAwsSdk}"
    implementation "com.amazonaws:aws-java-sdk-sqs:${revAwsSdk}"

    implementation "org.apache.commons:commons-lang3:"

    implementation "net.thisptr:jackson-jq:${revJq}"
    // SBMTODO: remove guava dep
    implementation "com.google.guava:guava:${revGuava}"

    implementation "javax.ws.rs:jsr311-api:${revJsr311Api}"

    implementation "org.apache.kafka:kafka-clients:${revKafka}"

    implementation "com.rabbitmq:amqp-client:${revAmqpClient}"

    implementation "io.nats:java-nats-streaming:${revNatsStreaming}"

    implementation "io.reactivex:rxjava:${revRxJava}"

    implementation "com.netflix.spectator:spectator-reg-metrics3:${revSpectator}"
    implementation "com.netflix.spectator:spectator-reg-micrometer:${revSpectator}"
	implementation "io.prometheus:simpleclient:${revPrometheus}"
	implementation "io.micrometer:micrometer-registry-prometheus:${revMicrometer}"

    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation "org.testcontainers:mockserver:${revTestContainer}"
    testImplementation "org.mock-server:mockserver-client-java:${revMockServerClient}"

    testImplementation project(':conductor-test-util').sourceSets.test.output
}

shadowJar {

    configurations = [project.configurations.shadow]
    classifier = null
    dependencies {
        include(dependency {
            it.moduleGroup == 'com.netflix.conductor' &&
                    (it.moduleName == 'conductor-task'
                            || it.moduleName == 'conductor-sqs'
                            || it.moduleName == 'conductor-nats'
                            || it.moduleName == 'conductor-amqp'
                            || it.moduleName == 'conductor-local-lock'
                            || it.moduleName == 'conductor-metrics'
                            || it.moduleName == 'conductor-noop'
                            || it.moduleName == 'conductor-s3-storage'
                    )
        })
    }
    // Service files are not included by default.
    mergeServiceFiles {
        include 'META-INF/services/*'
        include 'META-INF/maven/*'
    }
}

jar.enabled = false
jar.dependsOn shadowJar